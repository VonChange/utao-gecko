buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.yanzhenjie.andserver:plugin:2.1.12'
    }
}
plugins {
    alias(libs.plugins.android.application)
}
ext{
    geckoviewChannel = ""
    //-armeabi-v7a
    //79.0.20200813192915 89.0.20210614221319(可自动播放16 声音自动) 99.0.20220411174855  109.0.20230127170202
    //115.0.20230726201356 117.0.20230912013654 118.0.20231009140911(自动播放Override不一样 最高4.1)
    //119.0.20231108221453(21)  119.0.20231016204538(21) 130.0.20240913135723 140.0.20250616215311 139.0.20250609112858
    //141.0.20250714153642 143.0.20250929153833
    geckoviewVersion = "143.0.20250929153833"
    rxhttp_version = "3.2.6"
    okhttp_version = "3.12.13"
    room_version = "2.6.1"
    andserver_version = "2.1.12"
}
android {
    namespace 'com.vonchange.utao.gecko'
    compileSdk 35
    configurations {
        all {
            //exclude group: 'com.android.support', module: 'support-v4'
        }
    }
    buildFeatures {
        dataBinding = true
        viewBinding = true
    }
    defaultConfig {
        multiDexEnabled true
        applicationId "com.vonchange.utao.gecko"
        minSdk 21
        //noinspection ExpiredTargetSdkVersion 16 28
        targetSdk 28
        versionCode 9
        versionName "2025年10月30日"
        // ABI 分包由 splits 控制
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
                /*
                Room 具有以下注释处理器选项：
                room.schemaLocation：配置并启用将数据库架构导出到给定目录中的 JSON 文件的功能。
                                    [创建好表和数据库后编译，会在 app/schemas 目录下生成这个 JSON 文件]
                room.incremental：启用 Gradle 增量注释处理器。
                room.expandProjection：配置 Room 以重写查询，使其顶部星形投影在展开后仅包含 DAO 方法返回类型中定义的列。
                */
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
    // 针对ABI分别打包：armeabi-v7a、x86、x86_64
    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'x86', 'x86_64','arm64-v8a'
            universalApk false
        }
    }

    // 重命名输出：
    // release 使用中文名：utao火狐版安卓5以上-<versionName>-<abi>.apk
    // debug 使用纯英文名，且不包含 versionName（用 versionCode 代替），避免中文导致的ADB/文件系统问题
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def abiFilter = null
            try {
                abiFilter = output.getFilter(com.android.build.OutputFile.ABI)
            } catch (Throwable ignore) {}
            def abiName = (abiFilter == null) ? "universal" : abiFilter
            def isRelease = variant.buildType.name == 'release'
            if (isRelease) {
                if (abiName == 'x86_64') {
                    outputFileName = "utao火狐版PC模拟器专用-${variant.versionName}-x86(64位).apk"
                }
                if (abiName == 'x86') {
                    outputFileName = "utao火狐版PC模拟器专用-${variant.versionName}-x86.apk"
                }
                if (abiName == 'armeabi-v7a'){
                    outputFileName = "utao火狐版安卓5以上-${variant.versionName}-v7a.apk"
                }
                if (abiName == 'arm64-v8a'){
                    outputFileName = "utao火狐版安卓5以上-${variant.versionName}-v8a(手机用).apk"
                }
            } else {
                if (abiName == 'x86_64') {
                    outputFileName = "utao-fox-debug-pc-${variant.versionCode}-x86_64.apk"
                }
                if (abiName == 'x86') {
                    outputFileName = "utao-fox-debug-pc-${variant.versionCode}-x86.apk"
                }
                if(abiName == 'armeabi-v7a'){
                    outputFileName = "utao-fox-debug-${variant.versionCode}-v7a.apk"
                }
                if (abiName == 'arm64-v8a'){
                    outputFileName = "utao-fox-debug-${variant.versionCode}-v8a.apk"
                }
            }
        }
    }
}
dependencies {
   // implementation 'androidx.leanback:leanback:1.0.0'
    implementation "org.mozilla.geckoview:geckoview${geckoviewChannel}:${geckoviewVersion}"
    implementation "androidx.room:room-runtime:$room_version"
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    //implementation ('com.android.support:gridlayout-v7:22.+')
   // implementation 'androidx.percentlayout:percentlayout:1.0.0'
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation 'androidx.multidex:multidex:2.0.1'
    //implementation libs.appcompat
   // implementation libs.material
    testImplementation libs.junit

    //androidTestImplementation libs.ext.junit
   // androidTestImplementation libs.espresso.core
}

